entrypoints Prog;

Program . Prog ::= [Def] Exp ;

terminator Def ";" ;

DValue . Def ::= "let" Ident "=" Exp ;
DFunc  . Def ::= "let" Ident [Ident] "=" [Def] Exp;

terminator nonempty Ident " " ;

EIf  . Exp1 ::= "if" Exp2 "then" Exp2 "else" Exp2 "fi" ;

EAnd . Exp2 ::= Exp3 "&&" Exp3 ;
EOr  . Exp2 ::= Exp3 "||" Exp3 ;
EXor . Exp2 ::= Exp3 "^" Exp3 ;

EEq . Exp3 ::= Exp4 "==" Exp4 ;
ENe . Exp3 ::= Exp4 "!=" Exp4 ;
ELe . Exp3 ::= Exp4 "<" Exp4 ;
EGe . Exp3 ::= Exp4 ">" Exp4 ;

EPlus  . Exp4 ::= Exp4 "+" Exp5 ;
EMinus . Exp4 ::= Exp4 "-" Exp5 ;

ETimes . Exp5 ::= Exp5 "*" Exp6 ;
EDiv   . Exp5 ::= Exp5 "/" Exp6 ;

EApp . Exp6 ::= Ident [Exp7] ;

EVar        . Exp7 ::= Ident ;
EInt        . Exp7 ::= Integer ;
EBool_true  . Exp7 ::= "true" ;
EBool_false . Exp7 ::= "false" ;

separator nonempty Exp7 " " ;

coercions Exp 7;

comment "//" ;
comment "#" ;
comment "/*" "*/" ;
